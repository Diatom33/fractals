// Finalize compute shader.
// Reads accumulated weighted color (rgb + weight), divides by total weight,
// packs result to RGBA u32 for the output buffer / texture copy.
// Both escape-time and basin coloring use the same path since colorize.wgsl
// now does color-domain averaging for both modes.

struct Params {
    center_hi: vec2<f32>,
    center_lo: vec2<f32>,
    pixel_step: vec2<f32>,
    resolution: vec2<u32>,
    max_iter: u32,
    fractal_type: u32,
    julia_c: vec2<f32>,
    power: f32,
    relaxation: f32,
    color_mode: u32,
    num_roots: u32,
    sample_offset: vec2<f32>,
    sample_weight: f32,
    stride: u32,
    palette: u32,
    _pad: u32,
}

@group(0) @binding(0) var<uniform> params: Params;
@group(0) @binding(1) var<storage, read> accum: array<vec4<f32>>;
@group(0) @binding(2) var<storage, read_write> output: array<u32>;

fn pack_rgba(rgb: vec3<f32>) -> u32 {
    let r = u32(clamp(rgb.x * 255.0, 0.0, 255.0));
    let g = u32(clamp(rgb.y * 255.0, 0.0, 255.0));
    let b = u32(clamp(rgb.z * 255.0, 0.0, 255.0));
    return r | (g << 8u) | (b << 16u) | (255u << 24u);
}

@compute @workgroup_size(16, 16)
fn main(@builtin(global_invocation_id) gid: vec3<u32>) {
    let x = gid.x;
    let y = gid.y;
    let w = params.resolution.x;
    let h = params.resolution.y;
    if x >= w || y >= h { return; }

    let idx = y * params.stride + x;
    let a = accum[idx];
    let weight = a.w;

    var color: vec3<f32>;
    if weight > 0.0 {
        color = clamp(a.xyz / weight, vec3<f32>(0.0), vec3<f32>(1.0));
    } else {
        color = vec3<f32>(0.0);
    }

    output[idx] = pack_rgba(color);
}
